task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'gwt'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'com.palantir.gerrit.gerritci'
version = '0.1.0'

description = """gerrit-ci"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

/*
 * The provided configuration will designate dependencies that are required for compile and
 * runtime but should not be exported to the JAR.
 */
configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

/*
 * Configure the eclipse plugin to use the new provided configuration and download all
 * sources and javadoc JARs.
 */
eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
        downloadSources = true
        downloadJavadoc = true
    }
}

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/mass/maven/" }
    mavenCentral()
    maven { url "https://repo.jenkins-ci.org/public/" }
}

jar {
    from 'build/gwt/include'

    // Gerrit plugins are not supposed to have versions in the filename
    archiveName 'gerrit-ci.jar'

    manifest {
        attributes(
            'Gerrit-PluginName': project.name,
            'Gerrit-ApiType': apiType,
            'Gerrit-ApiVersion': apiVersion,
            'Gerrit-Module': 'com.palantir.gerrit.gerritci.Module',
            'Gerrit-HttpModule': 'com.palantir.gerrit.gerritci.HttpModule',
            'Gerrit-ReloadMode': 'reload',
            'Implementation-Vendor': 'Palantir',
            'Implementation-URL': 'https://github.com/palantir/gerrit-ci',
            'Implementation-Title': "${apiType} ${project.name}",
            'Implementation-Version': version,
        )
    }

    // Include compile dependencies in JAR.
    doFirst {
        from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    provided(
        [group: 'com.google.gerrit', name: "gerrit-${apiType}-api", version: apiVersion],
        [group: 'com.google.gerrit', name: 'gerrit-plugin-gwtui', version: apiVersion],
        [group: 'com.google.gwt', name:'gwt-servlet', version: '2.6.1']
    )

    compile(
        // We are stuck using the custom alpha version of jenkins-client until changes are merged upstream
        [group: 'com.offbytwo.jenkins', name: 'jenkins-client', version: '0.3.1-alpha'],
        [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'],
        [group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.12'],
        [group: 'log4j', name: 'log4j', version: '1.2.17'],
        [group: 'org.apache.velocity', name:'velocity', version: '1.7'],
        [group: 'org.jsoup', name: 'jsoup', version: '1.8.2']
    )

    testCompile(
        [group: 'junit', name: 'junit', version: '4.12']
    )
}

gwt {
    gwtVersion = '2.6.1'
    modules 'com.palantir.gerrit.gerritci.ui.projects'

    compiler {
        disableClassMetadata = true
        disableCastChecking = true
    }
}

// Moves output of GWT compilation under a /static directory so it's packed into the JAR correctly
task copyGwt(type:Copy) {
    from 'build/gwt/out'
    into 'build/gwt/include/static'
}
copyGwt.dependsOn compileGwt
copyGwt.mustRunAfter compileGwt
build.dependsOn copyGwt
build.mustRunAfter copyGwt

task reload(type:Exec) {
    commandLine "./scripts/reload-plugin.sh"
}
reload.dependsOn build
reload.mustRunAfter build

task all(dependsOn: [build, reload])

